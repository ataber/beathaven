$(document).on "click", "#show-billing-form", (event) ->
  event.preventDefault()
  $("#performer-submit").data("stripesubmit", false)
  $(".billing").find("input").each ->
    $(@).prop("disabled", false)

$("#performer-form").ready ->
  Stripe.setPublishableKey("<%= ENV["STRIPE_PUBLISHABLE_KEY"] %>")

  validateForm = ->
    $("#performer-submit").prop("disabled", false)
    $("#performer-form").validate({
      errorClass: "text-danger",
      rules: {
        legal_billing_name: "required",
        routing_number: "required",
        account_number: "required",
        billing_email: {
          required: true,
          email: true
        }
      },
      messages: {
        legal_billing_name: "Please specify legal billing name",
        routing_number: "Please specify routing number",
        account_number: "Please specify account number",
        billing_email: {
          required: "Please specify billing email",
          email: 'Billing email must be in email format'
        }
      },
      submitHandler: submitHandler
    })

  submitHandler = (form) ->
    $("#performer-submit").prop("disabled", true)
    if $("#performer-submit").data("stripesubmit")
      routingNumber = $(".routing-number").val()
      accountNumber = $(".account-number").val()
      Stripe.bankAccount.createToken({
        country: "US",
        routingNumber: routingNumber,
        accountNumber: accountNumber
      }, stripeResponseHandler)
    else
      $("#performer-form").get(0).submit()

  stripeResponseHandler = (status, response) ->
    if response.error
      $("#performer-submit").prop("disabled", false)
      $(".payment-errors").text(response.error.message)
    else
      payment_form = $("#performer-form")
      token = response["id"]
      payment_form.append($("<input type='hidden' name='stripeToken' />").val(token))
      payment_form.get(0).submit()

  validateForm()
